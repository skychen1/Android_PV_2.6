apply plugin: 'com.android.library'
//apply from: 'gradle-mvn-push.gradle'

version = VERSION_NAME
group = GROUP

android {
    compileSdkVersion 28
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28
        consumerProguardFiles 'consumer-proguard-rules.pro'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    testBuildType 'release'
    signingConfigs {
        release {
            storeFile new File("$System.env.HOME/.android/debug.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            if (isDebugBuild()) {
                debuggable true
                jniDebuggable true
            }
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

}

dependencies {
    implementation "pl.droidsonroids:relinker:1.3.1"
    compileOnly "androidx.annotation:annotation:1.0.0"
    testCompileOnly "androidx.annotation:annotation:1.0.0"
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:3.0.0"
    testImplementation "org.robolectric:robolectric:4.3"
    testImplementation "org.assertj:assertj-core:3.13.2"
    testImplementation "net.jodah:concurrentunit:0.4.4"
    testImplementation "org.khronos:opengl-api:gl1.1-android-2.1_r1"
    androidTestImplementation "junit:junit:4.12"
    androidTestImplementation "org.assertj:assertj-core:1.7.1"
    androidTestImplementation "androidx.test.ext:junit:1.1.0"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:4.1.1"
    androidTestImplementation "androidx.test:rules:1.2.0"
    androidTestImplementation "androidx.test:runner:1.2.0"
    androidTestCompileOnly "androidx.annotation:annotation:1.0.0"
}

def isDebugBuild() {
    !gradle.startParameter.taskNames.contains("uploadArchives") && VERSION_NAME.contains("SNAPSHOT")
}

task archiveSymbols(type: Tar) {
    from new File(buildDir, 'intermediates/cmake/release/obj')
    baseName = "$POM_ARTIFACT_ID-symbols"
    destinationDir = new File(buildDir, 'libs')
    extension = 'tar.gz'
    compression = Compression.GZIP
}